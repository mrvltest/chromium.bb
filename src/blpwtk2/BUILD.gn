# Copyright (C) 2017 Bloomberg L.P. All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
# 1. Redistributions of source code must retain the above copyright
#    notice, this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright
#    notice, this list of conditions and the following disclaimer in the
#    documentation and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
# "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
# LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
# A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
# OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
# SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
# LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
# DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
# THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

import("//build/config/chrome_build.gni")
import("//build/config/win/visual_studio_version.gni")
import("blpwtk2.gni")
import("//tools/grit/repack.gni")

config("blpwtk2_public") {
  include_dirs = [
    "$target_gen_dir/blpwtk2/public",
    "$target_gen_dir/blpv8/public",
    "$target_gen_dir/blppdfutil/public",
  ]
}

config("blpwtk2_shared") {
  defines = [ "USING_BLPWTK2_SHARED" ]
}

config("blpwtk2_gen_map") {
  ldflags = [
    "/MAP",
    "/MAPINFO:EXPORTS",
  ]
}

action("blpwtk2_generate_sources") {
  script = "//blpwtk2/gen_version.py"
  _output_blpwtk2_products_h = "$target_gen_dir/$blpwtk2_products_h"
  _output_blpv8_products_h = "$target_gen_dir/$blpv8_products_h"
  _output_blppdfutil_products_h = "$target_gen_dir/$blppdfutil_products_h"
  _output_version_h = "$target_gen_dir/$version_h"
  _output_version_cc = "$target_gen_dir/$version_cc"

  outputs = [
    _output_blpwtk2_products_h,
    _output_blpv8_products_h,
    _output_blppdfutil_products_h,
    _output_version_h,
    _output_version_cc,
  ]

  args = [
    "--output-blpwtk2-products",
    rebase_path(_output_blpwtk2_products_h, root_build_dir),
    "--output-blpv8-products",
    rebase_path(_output_blpv8_products_h, root_build_dir),
    "--output-blppdfutil-products",
    rebase_path(_output_blppdfutil_products_h, root_build_dir),
    "--output-version-h",
    rebase_path(_output_version_h, root_build_dir),
    "--output-version-cc",
    rebase_path(_output_version_cc, root_build_dir),
    "--version",
    bb_version,
  ]
}

shared_library("blpwtk2") {
  if (bb_version != "") {
    output_name = "blpwtk2.$bb_version"
  }

  sources = [
    "$target_gen_dir/$blpwtk2_products_h",
    "$target_gen_dir/$version_cc",
    "$target_gen_dir/$version_h",
    "private/blpwtk2.rc",
    "private/blpwtk2_browsercontextimpl.cc",
    "private/blpwtk2_browsercontextimpl.h",
    "private/blpwtk2_browsercontextimplmanager.cc",
    "private/blpwtk2_browsercontextimplmanager.h",
    "private/blpwtk2_browsermainrunner.cc",
    "private/blpwtk2_browsermainrunner.h",
    "private/blpwtk2_browserthread.cc",
    "private/blpwtk2_browserthread.h",
    "private/blpwtk2_channelinfo.cc",
    "private/blpwtk2_channelinfo.h",
    "private/blpwtk2_contentbrowserclientimpl.cc",
    "private/blpwtk2_contentbrowserclientimpl.h",
    "private/blpwtk2_contentmaindelegateimpl.cc",
    "private/blpwtk2_contentmaindelegateimpl.h",
    "private/blpwtk2_contentrendererclientimpl.cc",
    "private/blpwtk2_contentrendererclientimpl.h",
    "private/blpwtk2_contentutilityclientimpl.cc",
    "private/blpwtk2_contentutilityclientimpl.h",
    "private/blpwtk2_contextmenuitemimpl.cc",
    "private/blpwtk2_contextmenuitemimpl.h",
    "private/blpwtk2_contextmenuparamsimpl.cc",
    "private/blpwtk2_contextmenuparamsimpl.h",
    "private/blpwtk2_control_messages.h",
    "private/blpwtk2_desktopstreamsregistry.cc",
    "private/blpwtk2_desktopstreamsregistry.h",
    "private/blpwtk2_devtoolsfrontendhostdelegateimpl.cc",
    "private/blpwtk2_devtoolsfrontendhostdelegateimpl.h",
    "private/blpwtk2_devtoolshttphandlerdelegateimpl.cc",
    "private/blpwtk2_devtoolshttphandlerdelegateimpl.h",
    "private/blpwtk2_dllmain.cc",
    "private/blpwtk2_enumtraits.h",
    "private/blpwtk2_filechooserparamsimpl.cc",
    "private/blpwtk2_filechooserparamsimpl.h",
    "private/blpwtk2_findonpage.cc",
    "private/blpwtk2_findonpage.h",
    "private/blpwtk2_inprocessrenderer.cc",
    "private/blpwtk2_inprocessrenderer.h",
    "private/blpwtk2_inprocessresourceloaderbridge.cc",
    "private/blpwtk2_inprocessresourceloaderbridge.h",
    "private/blpwtk2_ipcparamtraits.cc",
    "private/blpwtk2_ipcparamtraits.h",
    "private/blpwtk2_jswidget.cc",
    "private/blpwtk2_jswidget.h",
    "private/blpwtk2_mainmessagepump.cc",
    "private/blpwtk2_mainmessagepump.h",
    "private/blpwtk2_managedrenderprocesshost.cc",
    "private/blpwtk2_managedrenderprocesshost.h",
    "private/blpwtk2_message_generator.cc",
    "private/blpwtk2_message_generator.h",
    "private/blpwtk2_nativeviewwidget.cc",
    "private/blpwtk2_nativeviewwidget.h",
    "private/blpwtk2_nativeviewwidgetdelegate.cc",
    "private/blpwtk2_nativeviewwidgetdelegate.h",
    "private/blpwtk2_networkdelegateimpl.cc",
    "private/blpwtk2_networkdelegateimpl.h",
    "private/blpwtk2_prefstore.cc",
    "private/blpwtk2_prefstore.h",
    "private/blpwtk2_processclient.cc",
    "private/blpwtk2_processclient.h",
    "private/blpwtk2_processclientimpl.cc",
    "private/blpwtk2_processclientimpl.h",
    "private/blpwtk2_processhost.cc",
    "private/blpwtk2_processhost.h",
    "private/blpwtk2_processhostimpl.cc",
    "private/blpwtk2_processhostimpl.h",
    "private/blpwtk2_processhostlistener.cc",
    "private/blpwtk2_processhostlistener.h",
    "private/blpwtk2_processhostmanager.cc",
    "private/blpwtk2_processhostmanager.h",
    "private/blpwtk2_profile_messages.h",
    "private/blpwtk2_profilehost.cc",
    "private/blpwtk2_profilehost.h",
    "private/blpwtk2_profileproxy.cc",
    "private/blpwtk2_profileproxy.h",
    "private/blpwtk2_proxyconfigimpl.cc",
    "private/blpwtk2_proxyconfigimpl.h",
    "private/blpwtk2_rendererinfomap.cc",
    "private/blpwtk2_rendererinfomap.h",
    "private/blpwtk2_rendererutil.cc",
    "private/blpwtk2_rendererutil.h",
    "private/blpwtk2_renderviewobserverimpl.cc",
    "private/blpwtk2_renderviewobserverimpl.h",
    "private/blpwtk2_resourcecontextimpl.cc",
    "private/blpwtk2_resourcecontextimpl.h",
    "private/blpwtk2_statics.cc",
    "private/blpwtk2_statics.h",
    "private/blpwtk2_toolkitimpl.cc",
    "private/blpwtk2_toolkitimpl.h",
    "private/blpwtk2_urlrequestcontextgetterimpl.cc",
    "private/blpwtk2_urlrequestcontextgetterimpl.h",
    "private/blpwtk2_utility.cc",
    "private/blpwtk2_utility.h",
    "private/blpwtk2_viewsdelegateimpl.cc",
    "private/blpwtk2_viewsdelegateimpl.h",
    "private/blpwtk2_webcontentsviewdelegateimpl.cc",
    "private/blpwtk2_webcontentsviewdelegateimpl.h",
    "private/blpwtk2_webframeimpl.cc",
    "private/blpwtk2_webframeimpl.h",
    "private/blpwtk2_webview_messages.h",
    "private/blpwtk2_webviewhost.cc",
    "private/blpwtk2_webviewhost.h",
    "private/blpwtk2_webviewimpl.cc",
    "private/blpwtk2_webviewimpl.h",
    "private/blpwtk2_webviewimplclient.cc",
    "private/blpwtk2_webviewimplclient.h",
    "private/blpwtk2_webviewproperties.cc",
    "private/blpwtk2_webviewproperties.h",
    "private/blpwtk2_webviewproxy.cc",
    "private/blpwtk2_webviewproxy.h",
    "public/blpwtk2.h",
    "public/blpwtk2_blob.cc",
    "public/blpwtk2_blob.h",
    "public/blpwtk2_config.h",
    "public/blpwtk2_constants.cc",
    "public/blpwtk2_constants.h",
    "public/blpwtk2_contextmenuitem.cc",
    "public/blpwtk2_contextmenuitem.h",
    "public/blpwtk2_contextmenuparams.cc",
    "public/blpwtk2_contextmenuparams.h",
    "public/blpwtk2_filechooserparams.cc",
    "public/blpwtk2_filechooserparams.h",
    "public/blpwtk2_iatpatchfunction.cc",
    "public/blpwtk2_iatpatchfunction.h",
    "public/blpwtk2_newviewparams.cc",
    "public/blpwtk2_newviewparams.h",
    "public/blpwtk2_pdfutil.cc",
    "public/blpwtk2_pdfutil.h",
    "public/blpwtk2_profile.cc",
    "public/blpwtk2_profile.h",
    "public/blpwtk2_profilecreateparams.cc",
    "public/blpwtk2_profilecreateparams.h",
    "public/blpwtk2_proxyconfig.cc",
    "public/blpwtk2_proxyconfig.h",
    "public/blpwtk2_pumpmode.cc",
    "public/blpwtk2_pumpmode.h",
    "public/blpwtk2_resourcecontext.cc",
    "public/blpwtk2_resourcecontext.h",
    "public/blpwtk2_resourceloader.cc",
    "public/blpwtk2_resourceloader.h",
    "public/blpwtk2_spellcheckconfig.cc",
    "public/blpwtk2_spellcheckconfig.h",
    "public/blpwtk2_string.cc",
    "public/blpwtk2_string.h",
    "public/blpwtk2_stringref.cc",
    "public/blpwtk2_stringref.h",
    "public/blpwtk2_textdirection.cc",
    "public/blpwtk2_textdirection.h",
    "public/blpwtk2_threadmode.cc",
    "public/blpwtk2_threadmode.h",
    "public/blpwtk2_toolkit.cc",
    "public/blpwtk2_toolkit.h",
    "public/blpwtk2_toolkitcreateparams.cc",
    "public/blpwtk2_toolkitcreateparams.h",
    "public/blpwtk2_toolkitfactory.cc",
    "public/blpwtk2_toolkitfactory.h",
    "public/blpwtk2_webcontentsettingsdelegate.cc",
    "public/blpwtk2_webcontentsettingsdelegate.h",
    "public/blpwtk2_webframe.cc",
    "public/blpwtk2_webframe.h",
    "public/blpwtk2_webview.cc",
    "public/blpwtk2_webview.h",
    "public/blpwtk2_webviewcreateparams.cc",
    "public/blpwtk2_webviewcreateparams.h",
    "public/blpwtk2_webviewdelegate.cc",
    "public/blpwtk2_webviewdelegate.h",
    "public/blpwtk2_webviewhostobserver.cc",
    "public/blpwtk2_webviewhostobserver.h",
  ]

  defines = [
    "BUILDING_BLPWTK2_SHARED",
    "BLPWTK2_IMPLEMENTATION",
  ]

  include_dirs = [
    "public",
    "private",
    ".",
  ]

  public_configs = [
    ":blpwtk2_public",
    ":blpwtk2_shared",
    "//third_party/angle:commit_id_config",
    "//base/allocator:allocator_shim_define",
  ]

  if (bb_generate_map_files) {
    public_configs += [ ":blpwtk2_gen_map" ]
  }

  public_deps = [
    ":blpwtk2_generate_sources",
    "//v8",
  ]

  deps = [
    "//base",
    "//chrome/chrome_blpwtk2",
    "//components/devtools_http_handler",
    "//content",
    "//content:resources",
    "//content:sandbox_helper_win",
    "//content/app/resources",
    "//ipc",
    "//net",
    "//net:extras",
    "//sandbox",
    "//skia",
    "//storage/common",
    "//third_party/WebKit/public:blink",
    "//third_party/WebKit/public:blink_minimal",
    "//third_party/WebKit/public:inspector_resources_grit",
    "//third_party/WebKit/public:resources",
    "//ui/aura",
    "//ui/base",
    "//ui/events",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/gfx/ipc",
    "//ui/gl",
    "//ui/resources",
    "//ui/shell_dialogs",
    "//ui/views",
    "//url",
  ]
}

executable("blpwtk2_subprocess") {
  if (bb_version != "") {
    output_name = "blpwtk2_subprocess.$bb_version"
  }

  sources = [
    "subprocess/main.cc",
    "subprocess/resources.rc",
  ]

  configs += [
    ":blpwtk2_public",
    "//build/config/win:windowed",
  ]

  if (bb_generate_map_files) {
    configs += [ ":blpwtk2_gen_map" ]
  }

  deps = [
    ":blpwtk2_generate_sources",
    "//content:sandbox_helper_win",
  ]
}

executable("blpwtk2_shell") {
  sources = [
    "shell/main.cc",
  ]

  include_dirs = [ "public" ]

  configs += [
    "//blppdfutil:blppdfutil_public_cfg",
    "//build/config/win:windowed",
  ]
  if (is_official_build) {
    configs -= [ "//build/config/compiler:default_optimization" ]
    configs += [ "//build/config/compiler:optimize_max" ] 
  }

  deps = [
    ":blpwtk2",
    "//blppdfutil:blppdfutil",
    "//base/allocator",
  ]

}

repack("blpwtk2_devtools") {
  if (bb_version != "") {
    output = "${root_out_dir}/blpwtk2_devtools.$bb_version.pak"
  } else {
    output = "${root_out_dir}/blpwtk2_devtools.pak"
  }

  sources = [
    "$root_gen_dir/blink/devtools_resources.pak",
    "$root_gen_dir/blink/public/resources/blink_inspector_resources.pak",
  ]

  deps = [
    "//content/browser/devtools:devtools_resources",
    "//third_party/WebKit/public:inspector_resources_grit",
  ]
}

group("blpwtk2_all") {
  testonly = true

  deps = [
    ":blpwtk2_devtools",
    ":blpwtk2_shell",
    ":blpwtk2_subprocess",
    "//blppdfutil",
    "//chrome/chrome_blpwtk2",
    "//v8:d8",

    "//content/shell:content_shell"
  ]

  if (bb_ppapi_examples) {
    deps += [
      "//ppapi/examples/2d:graphics_2d",
      "//ppapi/examples/2d:paint_manager",
      "//ppapi/examples/2d:scroll",
      "//ppapi/examples/audio",
      "//ppapi/examples/audio_input",
      "//ppapi/examples/enumerate_devices",
      "//ppapi/examples/file_chooser",
      "//ppapi/examples/flash_topmost",
      "//ppapi/examples/font",
      "//ppapi/examples/gamepad",
      "//ppapi/examples/ime",
      "//ppapi/examples/input",
      "//ppapi/examples/mouse_cursor",
      "//ppapi/examples/mouse_lock",
      "//ppapi/examples/printing",
      "//ppapi/examples/scripting",
      "//ppapi/examples/stub:c_stub",
      "//ppapi/examples/stub:cc_stub",
      "//ppapi/examples/url_loader:file",
      "//ppapi/examples/url_loader:streaming",
    ]
  }
}
